# ###################################################################
#
# Mp3tag parsing for mofibo.com based on storytel.com, created by romano on 2020-11-28
# inspired by https://github.com/seanap/Audible.com-Search-by-Album
#
# Changes in the commits: https://github.com/romanoh/Mp3tag-Repository
# Last update: 27.12.2020 v.0.8
# 04.12.2020 - Fix sort album tag if the album is not from a series
# 27.12.2020 - Improve Lenght display 
# 28.01.2021 - Added mofibo support - danish analog to storytel
#
#C:\Users\xxxx\Desktop\Audiobooks\%albumartist%\%series%\%year% - %album%[ '['%series% %series-part%']']\%album% (%year%) ['['%series% %series-part%']' ]- %Narrator% 
# ####################################################################

[Name]=Mofibo based on Storytel [v.0.8-27.12.2020]
[BasedOn]=mofibo.com
[IndexUrl]=https://mofibo.com/dk/da/soeg-
[AlbumUrl]=https://mofibo.com
[WordSeperator]=+
[IndexFormat]=% ↓ Sprog%|% ↓ Bogtitel%|% ↓ Forfatter%|% ↓ Format%|%_url%
[SearchBy]=Indtast bogtitel eller forfatter||$regexp(%album%,'[- ]+cd ?\d+$',,1)||%s
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\xxxx\Desktop\mp3tagdebug.txt"	

#Only select the area we need instead of everyting.
findline "gridItem"
joinuntil "hiddenSearchPhrase"

replace "asAudio" "Audiobook"
replace "asEbook" "Ebook"

##This is the list of the seach results
do	
	# Sprog
	findinline "language=\"" 
	sayuntil "\""
	say "|" 
	
	# Bogtitel
	findinline "name=\"" 
	sayuntil "\""
	say "|" 
	
	# Forfatter
	findinline "authors=\"" 
	sayuntil "\""
	say "|" 

	# Format
	findinline "<span class=\"" 
	sayuntil "\""
	say "|" 

	#  Url		
	findinline "gridBookTitle" 
	findinline "href=\"" 
	sayuntil "\""
	#the last on remove the #	
	#say "|"
	
	saynewline
	 
   findinline "gridItem" 1 1

##Set max results == 99    
while "\"" 99  

[ParserScriptAlbum]=...
# ###################################################################
#					HERE WE PARSE THE INFO FROM MOFIBO
#					INTO THE GIVEN TAGS SUPPORTED BY ID3v1
#| Field        | Length      | Description                                                                              |
#|--------------|-------------|------------------------------------------------------------------------------------------|
#| header       | 3           | "TAG"                                                                                    |
#| title        | 30          | 30 characters of the title                                                               |
#| artist       | 30          | 30 characters of the artist name                                                         |
#| album        | 30          | 30 characters of the album name                                                          |
#| year         | 4           | A four-digit year                                                                        |
#| comment      | 28[7] or 30 | The comment.                                                                             |
#| zero-byte[7] | 1           | If a track number is stored, this byte contains a binary 0.                              |
#| track[7]     | 1           | The number of the track on the album, or 0. Invalid, if previous byte is not a binary 0. |
#| genre        | 1           | Index in a list of genres, or 255                                                        |
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.html"

# Cover (working in plex)
outputto "Coverurl"
findline "og:image"
findinline "content=\""
sayuntil "\""

# Bogtiltel
outputto "Title"
findline "show-on-mobile"
findline "name\">"
findinline "name\">"
sayuntil "</span>"

# Album Bogtiltel (working in plex)
outputto "Album"
findline "hide-on-mobile"
findline "name\">"
findinline "name\">"
sayuntil "</span>"

# Rating 
outputto "RATING WINAMP"
findline "dennisBookRating" 1 1
findinline "rating=\""
sayuntil "\""

# Rating WMP
outputto "RATING WMP"
sayoutput "RATING WINAMP"

# Author/Artist (working in plex)
outputto "Artist"
findline "expandAuthor"
findinline "expandAuthorName\">"
sayuntil "</span>"

# Oplæser (working in plex - but not as eg. Record Lable)
outputto "Narrator"
findline "expandReader"
findinline "expandReaderName\">"
sayuntil "</span>"

# Comment (working in Plex)
outputto "Description"
findline "description"
findinline "\">"
moveline 1
replace "<br />" " "
sayuntil "</span>"

# Comment and description same content
outputto "Comment"
sayoutput "Description"

#Language (added to Genre as it works in Plex working)
outputto "Genre"
sayoutput "Narrator"
say "; "
outputto "Genre"
findline "expanderLanguage"
findinline ":"
sayuntil "</b>"
say "; "

# Genre (working in Plex)
outputto "Category"
findline "itemprop=\"category\">"
findinline "category\">"
sayuntil "</span>"
outputto "Genre"
sayoutput "Category"
say "; "


# Series
outputto "SERIES"
findline "Serie:" 1 1
if "<b class=\"bookDetailListItem margin\">"
	findinline "/series/" 1 1
	findinline "\">" 1 1
	sayuntil ":"
	outputto "series-part"
	findinline ":" 1 1
	sayuntil "</a>"
else
	say ""
	gotoline 1
endif

#Oversætter (working)
outputto "Composer"
findline "bookDetailListItem"
moveline 2
findinline ":"
sayuntil "</b>"

#Publisher (working)
outputto "Publisher"
findline "Forlag:"
joinuntil "</span>"
findinline "\" itemprop=\"brand\">"
sayuntil "</a>"

# Copyright
outputto "COPYRIGHT"
sayoutput "Publisher"

# Year (working in Plex)
outputto "year"
findline "Udgivet:"
moveline 1
SayNextNumber

# Releasetime
outputto "RELEASETIME"
sayoutput "year"

# Lenght (working)
outputto "lenght"
findline "Længde:"
replace "H " ":"
replace "Min" ""
findinline "Længde:"
sayuntil "</b>"

#ISBN (working)
outputto "ISBN"
findline "ISBN: "
findinline "ISBN: "
saynextnumber

#Add more details to Genre
outputto "Genre"
findline "expandTagsTag" 1 1
if "<b"
	joinuntil "</div>"
do	
	findinline "\">" 2
	sayuntil "</a></b>"
	say "; "
		
	saynewline
	 
    findinline "expandTagsTag" 1 1
    
while "\"" 25
else
endif	

# Set "Albumsort"
IfOutput "series"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "series-part"
	say " "
	say "- "
	sayoutput "Year"
	say ". "
	sayoutput "album"
else
	outputto "Albumsort"
	sayoutput "Year"
	say ". "
	sayoutput "album"
endif

# Set Disknumber
outputto "DISCNUMBER"
sayoutput "series-part"

# Set Audible Album URL
outputto "WWWAUDIOFILE"
sayoutput "CurrentUrl"

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# Set iTunes Media Type to Audiobook
outputto "ITUNESMEDIATYPE"
say "Audiobook"

# Set iTunes Gapless Playback
outputto "ITUNESGAPLESS"
say "1"